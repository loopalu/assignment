plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'ee.tech'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
	targetCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	runtimeOnly project(':frontend')
}

tasks.named('test') {
	useJUnitPlatform()

	include '**/ee/tech/assignment/**/*Test.class'
	exclude '**/ee/tech/assignment/integration/**'

	outputs.upToDateWhen { false }

	afterSuite { desc, result ->
		if (!desc.parent) {
			println "\nUnit Test result: ${result.resultType}"
			println "Unit Test summary: ${result.testCount} tests, " +
					"${result.successfulTestCount} succeeded, " +
					"${result.failedTestCount} failed, " +
					"${result.skippedTestCount} skipped"
		}
	}
}

task integrationTest(type: Test) {
	useJUnitPlatform()

	include '**/ee/tech/assignment/integration/**/*Test.class'

	outputs.upToDateWhen { false }

	doFirst {
		println 'Ensure the application is running before executing E2E tests'
	}

	afterSuite { desc, result ->
		if (!desc.parent) {
			println "\nTest result: ${result.resultType}"
			println "Test summary: ${result.testCount} tests, " +
					"${result.successfulTestCount} succeeded, " +
					"${result.failedTestCount} failed, " +
					"${result.skippedTestCount} skipped"
		}
	}
}